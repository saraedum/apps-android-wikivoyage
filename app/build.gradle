apply plugin: 'com.android.application'

import com.android.build.gradle.internal.dsl.ProductFlavor
import com.android.ddmlib.DdmPreferences

import java.util.concurrent.TimeUnit

// Copy the signing.properties.sample file to ~/.sign/signing.properties and adjust the values.
final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)

final int ADB_TIMEOUT = TimeUnit.MINUTES.toMillis(15)
final boolean continuousIntegrationBuild = System.getenv('JENKINS_HOME') != null
final boolean preDexEnabled = hasProperty('pre.dex') ?
        Boolean.valueOf(getProperty('pre.dex').toString()) :
        !continuousIntegrationBuild
if (!preDexEnabled) {
    println 'Pre-dexing disabled.'
}

if (continuousIntegrationBuild) {
    DdmPreferences.setTimeOut(ADB_TIMEOUT)
    println "Device timeout is ${DdmPreferences.getTimeOut()}ms"
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

def computeVersionName(label) {
    return "2.6.${android.defaultConfig.versionCode}-${label}-${date}"
}

final JavaVersion JAVA_VERSION = JavaVersion.VERSION_1_7
android {
    // Keep version in sync with /project.properties
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    compileOptions {
        sourceCompatibility = JAVA_VERSION
        targetCompatibility = JAVA_VERSION
    }

    dexOptions {
        preDexLibraries = preDexEnabled
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 198
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                argument('butterknife.minSdk', minSdkVersion.apiString)
            }
        }
    }

    sourceSets {
        test {
            java.srcDirs += 'src/testlib/java'
        }
        androidTest {
            java.srcDirs += 'src/testlib/java'
        }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
    }

    flavorDimensions "backend", "deployment"

    productFlavors {
        dev {
            dimension "deployment"
            versionName computeVersionName('dev')
            applicationIdSuffix 'dev'
            signingConfig signingConfigs.debug
        }
        prod {
            dimension "deployment"
            versionName computeVersionName('r')
            signingConfig signingConfigs.prod
        }
        releasesprod {
            dimension "deployment"
            versionName computeVersionName('releasesprod')
            signingConfig signingConfigs.prod
        }
        alpha {
            dimension "deployment"
            versionName computeVersionName('alpha')
            applicationIdSuffix 'alpha'
            signingConfig signingConfigs.debug
        }
        beta {
            dimension "deployment"
            versionName computeVersionName('beta')
            applicationIdSuffix 'beta'
            signingConfig signingConfigs.prod
        }
        amazon {
            dimension "deployment"
            versionName computeVersionName('amazon')
            signingConfig signingConfigs.prod
        }
        fdroid {
            dimension "deployment"
            versionName computeVersionName('fdroid')
            signingConfig signingConfigs.prod
        }
        custom {
            dimension "deployment"
            versionName computeVersionName(customChannel)
            // next line is for injecting a custom channel value into the custom/AndroidManifest.xml
            manifestPlaceholders = [customChannel:getProperty('customChannel').toString()]
            signingConfig signingConfigs.prod
        }

        wikipedia {
            dimension "backend"
            applicationId 'org.wikipedia'
            testApplicationId 'org.wikipedia.test'
            testInstrumentationRunner 'org.wikipedia.test.AndroidTestRunner'
        }
        wikivoyage {
            dimension "backend"
            applicationId 'tm.info.drugtestbots.wikivoyage'
            testApplicationId 'tm.info.drugtestbots.wikivoyage.test'
            testInstrumentationRunner 'tm.info.drugtestbots.wikivoyage.test.AndroidTestRunner'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor' // required by Butter Knife

        // For Espresso testing libraries. See http://stackoverflow.com/q/33800924/970346.
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
    }
}

apply from: '../gradle/src/test.gradle'
apply from: '../gradle/src/checkstyle.gradle'
apply from: '../gradle/src/screenshot.gradle'

def compileSubprojectOrModule = { String projectName,
                                  String moduleName,
                                  String excludeGroup = null,
                                  String excludeModule = null ->
    def dependency = project.findProject(projectName) ?: moduleName
    project.dependencies.add('compile', dependency, {exclude group: excludeGroup, module: excludeModule })
}

dependencies {

    // To keep the Maven Central dependencies up-to-date
    // use http://gradleplease.appspot.com/ or http://search.maven.org/.
    // Debug with ./gradlew -q app:dependencies --configuration compile

    String okHttpVersion = '3.8.1' // When updating this version, resync file copies under
                                   // app/src/main/java/okhttp3
    String retrofitVersion = '2.3.0'
    String supportVersion = '25.4.0'
    String espressoVersion = '2.2.2'
    String butterKnifeVersion = '8.5.1'
    String frescoVersion = '1.4.0' // When updating this version, resync file copies under
                                   // app/src/main/java/com/facebook
    String testingSupportVersion = '0.5'
    String mockitoCore = 'org.mockito:mockito-core:1.9.5'
    String leakCanaryVersion = '1.5'

    compile "com.android.support:support-compat:$supportVersion"
    compile "com.android.support:support-core-utils:$supportVersion"
    compile "com.android.support:support-core-ui:$supportVersion"
    compile "com.android.support:support-fragment:$supportVersion"

    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:percent:$supportVersion"
    compile "com.android.support:palette-v7:$supportVersion"
    compile "com.android.support:preference-v14:$supportVersion"

    compile "com.facebook.fresco:animated-gif:$frescoVersion"
    compile "com.facebook.fresco:fresco:$frescoVersion"
    compile "com.facebook.fresco:imagepipeline-okhttp3:$frescoVersion"
    compile 'com.google.code.gson:gson:2.8.1'
    compile 'com.romandanylyk:pageindicatorview:0.1.2'
    compile "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion" // for JavaNetCookieJar
    compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    compile 'com.squareup:otto:1.3.8'
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile 'com.github.ryanjohn1:onboarding:1.0.3'
    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    compile('com.mapbox.mapboxsdk:mapbox-android-sdk:5.1.0@aar') {
        transitive = true
    }
    compile 'net.hockeyapp.android:HockeySDK:4.1.3'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'org.jsoup:jsoup:1.10.2'
    compile 'com.dmitrybrant:zimdroid:0.0.11'

    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    android.productFlavors.each { flavor ->
        String dep
        if('dev' == flavor.name) {
            dep = "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
        } else {
            dep = "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
        }
        "${flavor.name}Compile" dep
    }

    testCompile 'junit:junit:4.12'
    testCompile mockitoCore
    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile "com.squareup.okhttp3:mockwebserver:$okHttpVersion"

    androidTestCompile mockitoCore
    androidTestApk 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestCompile 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestApk 'com.crittercism.dexmaker:dexmaker-dx:1.4'

    // Required by Android JUnit Runner.
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile "com.android.support.test:rules:$testingSupportVersion" // JUnit Rules
    androidTestCompile "com.android.support.test:runner:$testingSupportVersion" // Android JUnit Runner
    androidTestCompile "com.android.support.test.espresso:espresso-core:$espressoVersion"
}

private setSigningConfigKey(config, Properties props) {
    if (props) {
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

@Nullable private Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}

addSyncContentProviderAuthority 'useroption', 'user_option'

private void addSyncContentProviderAuthority(String path, String name) {
    android.productFlavors.all { ProductFlavor flavor ->
        String authority = "${appId(flavor)}.sync.${path}"
        resValue 'string', "${name.toLowerCase()}_authority", authority
        buildConfigField 'String', "${name.toUpperCase()}_AUTHORITY", "\"$authority\""
    }
}

private String appId(ProductFlavor flavor) {
    String suffix = flavor.applicationIdSuffix
    String id = android.defaultConfig.applicationId + (suffix ? ".$suffix" : '')
    return id
}
